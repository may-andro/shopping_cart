plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.mayandro.shoppingcart"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "com.mayandro.shoppingcart.HiltTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    testOptions.unitTests {
        includeAndroidResources = true

        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }

    packagingOptions {
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }

}

dependencies {
    implementation project(path: ':remote')
    implementation project(path: ':utility')
    implementation project(path: ':domain')
    implementation project(path: ':local')
    implementation project(path: ':data')

    //Android
    def androidDependencies = rootProject.ext.androidDependencies
    implementation androidDependencies.appcompat
    implementation androidDependencies.constraintlayout
    implementation androidDependencies.material
    implementation androidDependencies.palette

    // Kotlin
    def kotlinDependencies = rootProject.ext.kotlinDependencies
    implementation kotlinDependencies.kotlin
    implementation kotlinDependencies.coroutines
    implementation kotlinDependencies.ktx

    //LifeCycle
    def lifecycleDependencies = rootProject.ext.lifecycleDependencies
    implementation lifecycleDependencies.lifecycleViewmodelKtx
    implementation lifecycleDependencies.lifecycleLivedataKtx
    implementation lifecycleDependencies.lifecycleViewmodelSavedState

    // Navigation
    def navigationDependencies = rootProject.ext.navigationDependencies
    implementation navigationDependencies.navigationFragment
    implementation navigationDependencies.navigationUiKtx
    implementation navigationDependencies.navigationDynamicFeatures
    androidTestImplementation navigationDependencies.navigationTesting

    //ROOM
    def dbDependencies = rootProject.ext.dbDependencies
    implementation dbDependencies.room
    kapt dbDependencies.roomCompiler
    implementation dbDependencies.roomKtx

    //RETROFIT
    def networkDependencies = rootProject.ext.networkDependencies
    implementation networkDependencies.gson
    implementation networkDependencies.loggingInterceptor
    implementation networkDependencies.retrofit
    implementation networkDependencies.retrofitConverter

    //GLIDE
    def glideDependencies = rootProject.ext.glideDependencies
    implementation glideDependencies.glide

    //PAGER3
    def pagerDependencies = rootProject.ext.pagerDependencies
    implementation pagerDependencies.paging

    //Hilt
    def hiltDependencies = rootProject.ext.hiltDependencies
    implementation hiltDependencies.hilt
    kapt hiltDependencies.daggerHiltCompiler
    // To integrate with ViewModel
    implementation hiltDependencies.hiltLifecycle
    kapt hiltDependencies.hiltCompiler


    testImplementation "io.mockk:mockk:1.10.6"
    androidTestImplementation "io.mockk:mockk-android:1.10.6"

    // Hilt testing
    androidTestImplementation "com.google.dagger:hilt-android-testing:2.33-beta"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:2.32-alpha"
    kaptAndroidTest "androidx.hilt:hilt-compiler:1.0.0-alpha03"

    // Local Unit Tests
    implementation "androidx.test:core:1.3.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.1"
    testImplementation "com.google.truth:truth:1.1"

    // Instrumented Unit Tests
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:2.12.1"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.1"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "com.google.truth:truth:1.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    debugImplementation "androidx.fragment:fragment-testing:1.3.0"

    androidTestImplementation ("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
        exclude group: "org.mockito", module: "mockito-core"
    }
}